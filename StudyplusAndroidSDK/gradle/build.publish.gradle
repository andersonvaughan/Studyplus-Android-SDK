apply plugin: 'maven'
apply plugin: 'signing'

signing {
    sign configurations.archives
}

task sdkJar(type: Jar) {
    from fileTree(dir: 'build/classes/release').matching { include 'jp/studyplus/android/sdk/**' }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.allJava
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.allSource
}

artifacts {
    archives sdkJar
    archives androidSourcesJar
    archives androidJavadocsJar
}

def requireInput = { message ->
    "${System.console().readPassword(message)}"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(
                    userName: requireInput("\nsonatype-username:"),
                    password: requireInput("sonatype-password:") )
            }
            pom.version = "1.0.2"
            pom.groupId = 'jp.studyplus.android.sdk'
            pom.artifactId = "studyplus-android-sdk"
            pom.project {
                name 'Studyplus Android SDK'
                description 'Studyplus SDK for Android'
                url 'https://github.com/studyplus/Studyplus-Android-SDK'
                scm {
                    def location = 'scm:git@github.com:studyplus/Studyplus-Android-SDK.git'
                    url location
                    connection location
                    developerConnection location
                }
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'x7c1'
                        name 'haco'
                    }
                }
            }
        }
    }
}
