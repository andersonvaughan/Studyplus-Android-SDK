
apply plugin: 'android-library'
apply plugin: 'android-test'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 16
        versionCode 1
        versionName "1.0"
    }
    sourceSets {
        instrumentTest.setRoot('src/test')
    }
}

tasks.withType(Test) {
    systemProperty "robolectric.logging", "stdout"
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.google.guava:guava:16.0'

    def forTesting = [
        'junit:junit:4.11',
        'org.robolectric:robolectric:2.3-SNAPSHOT',
        'com.squareup:fest-android:1.0.7',
    ]
    forTesting.each{ testCompile it }
    forTesting.each{ instrumentTestCompile it }
}

apply from: 'gradle/build.values.gradle'

if (gradle.startParameter.taskNames.contains('uploadArchives')) {
    apply from: 'gradle/build.publish.gradle'
}
android.libraryVariants.all { variant ->
    def name = variant.name
    task "javadoc-$name"(type: Javadoc) {
        description = "Generates javadoc for build $name"
        destinationDir = new File(destinationDir, variant.baseName)
        source = files(variant.javaCompile.source)
        classpath = files(android.plugin.runtimeJarList, variant.javaCompile.classpath)
        exclude '**/R.java'
    }
}
