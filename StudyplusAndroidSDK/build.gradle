
apply plugin: 'com.android.library'
apply plugin: 'robolectric'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

robolectric {
    // configure the set of classes for JUnit tests
    include '**/*Test.class'

    // Specify max number of processes (default is 1)
    maxParallelForks = 4

    // Specify max number of test classes to execute in a test process
    // before restarting the process (default is unlimited)
    forkEvery = 150

    // configure whether failing tests should fail the build
    ignoreFailures true

    // use afterTest to listen to the test execution results
    afterTest { descriptor, result ->
        println "Executing test for ${descriptor.name} with result: ${result.resultType}"
    }
}

tasks.withType(Test) {
    systemProperty "robolectric.logging", "stdout"
}

dependencies {
    compile 'com.google.guava:guava:16.0'

    androidTestCompile('junit:junit:4.11')
    androidTestCompile('org.robolectric:robolectric:2.4') {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }
    androidTestCompile('com.squareup:fest-android:1.0.7')
}

apply from: 'gradle/build.values.gradle'

apply from: 'gradle/build.publish.gradle'
