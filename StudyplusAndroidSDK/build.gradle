apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 16
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.google.guava:guava:16.0'
}

apply plugin: 'maven'
apply plugin: 'signing'

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(
                    userName: sonatypeUsername,
                    password: sonatypePassword)
            }
            pom.version = "1.0.0"
            pom.groupId = 'jp.studyplus.android.sdk'
            pom.artifactId = "studyplus-android-sdk"
            pom.project {
                name 'Studyplus Android SDK'
                packaging 'aar'
                description 'Studyplus SDK for Android'
                url 'https://github.com/studyplus/Studyplus-Android-SDK'
                scm {
                    def location = 'scm:git@github.com:studyplus/Studyplus-Android-SDK.git'
                    url location
                    connection location
                    developerConnection location
                }
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'x7c1'
                        name 'haco'
                    }
                }
            }
        }
    }
}

android.libraryVariants.all { variant ->
    def name = variant.name
    task "javadoc-$name"(type: Javadoc) {
        description = "Generates javadoc for build $name"
        destinationDir = new File(destinationDir, variant.baseName)
        source = files(variant.javaCompile.source)
        classpath = files(android.plugin.runtimeJarList, variant.javaCompile.classpath)
        exclude '**/R.java'
    }
}
